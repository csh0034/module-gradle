plugins {
    id 'org.springframework.boot' version '2.5.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'java'
    id 'java-library'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: 'java-library'

    group 'com.ask'
    version '1.0-SNAPSHOT'
    sourceCompatibility = 1.8

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        constraints {
            testImplementation 'org.dbunit:dbunit:2.7.2'
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.mariadb.jdbc:mariadb-java-client'

        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        developmentOnly 'org.springframework.boot:spring-boot-devtools'
    }

    test {
        useJUnitPlatform()
    }

    springBoot {
        buildInfo {
            // default destinationDir = file("$buildDir/resources/main/META-INF/build-info.properties")
            properties {
                time = null
                if (System.getenv('build_number') != null) {
                    additional = ['build_number': System.getenv('build_number')]
                }
            }
        }
    }
}

tasks {
    jar {
        enabled = false
    }
    bootJar {
        enabled = false
    }
    test {
        dependsOn ':module-core:test'
    }
    build {
        dependsOn ':module-core:build'
    }
}
