buildscript {
    ext {
        springBootVersion = '2.5.5'
        dependencyManagementVersion = '1.0.11.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'

    group 'com.ask'
    version '1.0-SNAPSHOT'
    sourceCompatibility = 1.8
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.mariadb.jdbc:mariadb-java-client'

        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        developmentOnly 'org.springframework.boot:spring-boot-devtools'
    }

    test {
        useJUnitPlatform()
    }

    springBoot {
        buildInfo {
            properties {
                time = null
                if (System.getenv('build_number') != null) {
                    additional = ['build_number': System.getenv('build_number')]
                }
            }
        }
    }
}

tasks {
    jar {
        enabled = false
    }
    test {
        dependsOn ':module-core:test'
    }
    build {
        dependsOn ':module-core:build'
    }
}